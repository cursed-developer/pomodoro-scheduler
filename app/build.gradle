plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'org.jetbrains.kotlin.plugin.compose'
}

android {
    signingConfigs {
        release {
            storeFile file(findProperty('pomodoro.storeFile') ?: 'mock')
            storePassword findProperty('pomodoro.storePassword') ?: ''
            keyPassword findProperty('pomodoro.keyPassword') ?: ''
            keyAlias findProperty('pomodoro.keyAlias') ?: ''
        }
    }
    compileSdk 34

    defaultConfig {
        applicationId "com.emenjivar.pomodoro"
        minSdk 23
        targetSdk 35
        versionCode 5
        versionName "1.0.3"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            // release.enableCrashlytics is set on github actions
            // to enable|disable according the type of pipeline
            ext.enableCrashlytics = findProperty('release.enableCrashlytics') ?: false
            //

            manifestPlaceholders = [enableCrashlytics:"${ext.enableCrashlytics}"]
            signingConfig signingConfigs.release
            buildConfigField "boolean", "enableCrashlytics", "$enableCrashlytics"
        }
        debug {
            debuggable true

            // debug.enableCrashlytics is set on github actions
            // to enable|disable according the type of pipeline
            ext.enableCrashlytics = findProperty('debug.enableCrashlytics') ?: false
            manifestPlaceholders = [enableCrashlytics:"${ext.enableCrashlytics}"]
            // applicationIdSuffix ".debug"
            buildConfigField "boolean", "enableCrashlytics", "$enableCrashlytics"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    testOptions {
        unitTests.all {
            kover {
                disabled = false // true to disable instrumentation tests of this task, Kover reports will not depend on the results of their execution
                excludes = [
                        "com.emenjivar.pomodoro.components.*",
                        "com.emenjivar.pomodoro.ui.theme.BuildConfig",
                        "com.emenjivar.pomodoro.ui.theme.MainActivity",
                        'com.emenjivar.pomodoro.ui.theme.*',
                        'com.emenjivar.pomodoro.di.*',
                ]
            }
        }
    }
    namespace 'com.emenjivar.pomodoro'
}

composeCompiler {
    reportsDestination = layout.buildDirectory.dir("compose_compiler")
    stabilityConfigurationFile = rootProject.layout.projectDirectory.file("stability_config.conf")
}

afterEvaluate {
    android.buildTypes.each { build ->
        println("buildName: ${build.name}, enableCrashlytics: ${build.ext.enableCrashlytics}")
        if (build.ext.enableCrashlytics == 'true') {
            println("Crashlytic enabled on ${build.name}")
            apply plugin: 'com.google.gms.google-services'
            apply plugin: 'com.google.firebase.crashlytics'
        }
    }
}

def core_version = "1.7.0"
def koin_version= "3.2.0-beta-1"
def mockito_version = "5.2.0"
def compose_material = "1.7.6"

dependencies {
    implementation project(':core')
    implementation project(':data')
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.appcompat:appcompat:1.7.0'

    // UI`
    implementation 'com.google.android.material:material:1.12.0'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_material"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.compose.ui:ui-viewbinding:$compose_version"
    implementation 'androidx.activity:activity-compose:1.9.3'
    implementation "androidx.constraintlayout:constraintlayout-compose:1.1.0"
    implementation "androidx.core:core-ktx:$core_version"


    // Lifecycle
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'

    // LiveData
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    implementation "androidx.compose.runtime:runtime:$compose_version"

    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.4.1"

    // DataStore
    implementation 'androidx.datastore:datastore-preferences:1.1.1'

    // Coroutine
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1'

    // Dependency injection - global library
    api "io.insert-koin:koin-core:$koin_version"
    api "io.insert-koin:koin-android:$koin_version"
    api "io.insert-koin:koin-androidx-compose:$koin_version"

    // Google
    implementation platform('com.google.firebase:firebase-bom:33.7.0')
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-core:21.1.1'
    implementation 'com.google.android.play:core:1.10.3'

    // Testing
    testImplementation 'junit:junit:'
    testImplementation "androidx.arch.core:core-testing:2.2.0"
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.9.0'
    testImplementation "org.mockito:mockito-core:$mockito_version"
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:1.7.6"
    androidTestImplementation "org.mockito:mockito-android:$mockito_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation("androidx.compose.ui:ui-test-manifest:$compose_version")
}